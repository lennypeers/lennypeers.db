# Maintainer: lennypeers <lennypeers+git@gmail.com>

pkgname=xf86-input-libinput-fds
pkgver=1.2.0
pkgrel=2
pkgdesc="Generic input driver for the X.Org server based on libinput, patched for 3 finger drag and 4 finger swipe"
arch=('x86_64')
license=('custom:MIT')
url="http://xorg.freedesktop.org/"
depends=('libinput')
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=24.4' 'libxi' 'libx11' 'xorgproto')
conflicts=('xorg-server<1.19.0' 'X-ABI-XINPUT_VERSION<24' 'X-ABI-XINPUT_VERSION>=25' 'xf86-input-libinput')
provides=('xf86-input-libinput=1.2.0')
groups=('xorg-drivers')
source=(
	https://xorg.freedesktop.org/releases/individual/driver/${pkgname%-*}-${pkgver}.tar.bz2{,.sig}
	http://0x0.st/osvb.patch
)
sha512sums=(
	'ebff9490d33fd7495df2e03203060a76b3274f0e638bc92f3d95257fac57cdb9dac308e90e54006fe9522b6de3b1f1c38fd5f0b0b75b7051e5422290f43de52d'
	'SKIP'
	'03659f7700b1274eccaa57125babc026a814a84a9737bcfcbd7ad27ff92550aa31f12cb1f862dbe3e148485653501b0dc8f49849e9cf75b1cd23c98f7e072a79'
)
validpgpkeys=('FD0004A26EADFE43A4C3F249C6F7AE200374452D') # "Povilas Kanapickas <povilas@radix.lt>"

prepare() {
  cd ${pkgname%-*}-${pkgver}
  patch -p1 -i "$srcdir/osvb.patch"
}

build() {
  cd ${pkgname%-*}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname%-*}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-*}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-*}/"
}
